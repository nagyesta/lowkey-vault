plugins {
    alias(libs.plugins.spring.boot)
    id 'java'
    alias(libs.plugins.lombok)
    alias(libs.plugins.abort.mission)
}

group = "${rootProject.group}"

project.ext {
    artifactDisplayName = "Lowkey Vault - App"
    artifactDescription = "Assembled application of Lowkey Vault."
}

dependencies {
    implementation libs.bundles.spring.boot.app
    implementation libs.bundles.logback
    implementation libs.bundles.tomcat
    implementation libs.bouncycastle.bcpkix
    implementation libs.hibernate.validator
    implementation libs.handlebars
    implementation libs.findbugs.jsr305
    implementation libs.springdoc.openapi.ui
    annotationProcessor libs.lombok
    annotationProcessor libs.spring.boot.configuration.processor
    testImplementation libs.bundles.spring.test
    testImplementation libs.jupiter
    testImplementation libs.abort.mission.jupiter
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

test {
    outputs.file(layout.buildDirectory.file("reports/abort-mission/abort-mission-report.json").get().getAsFile())
    useJUnitPlatform()
    systemProperty("junit.jupiter.extensions.autodetection.enabled", true)
    systemProperty("junit.jupiter.execution.parallel.enabled", true)
    systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
    systemProperty("junit.jupiter.execution.parallel.mode.classes.default", "concurrent")
}

abortMission {
    toolVersion libs.versions.abortMission.get()
}

processResources {
    inputs.property('version', project.version)
    filesMatching('application.properties') {
        expand(project.properties)
    }
}

jar {
    enabled = false
}

bootJar {
    archiveVersion.value(project.version as String)
}
tasks.resolveMainClassName.dependsOn("copyLegalDocs")

tasks.register('regenerateCertJks', Exec) {
    outputs.file("${project.projectDir}/src/main/resources/cert/keystore.jks")
    workingDir file("${project.projectDir}/src/main/resources/cert")
    outputs.upToDateWhen { false }

    //generate key in JKS with JDK15+ keytool to allow wildcard SAN
    commandLine "${project.property('keyToolDir') ?: ''}/keytool",
            '-genkeypair',
            '-alias', 'lowkey-vault.local',
            '-keyalg', 'RSA',
            '-keysize', '4096',
            '-validity', '3650',
            '-dname', 'CN=lowkey-vault.local',
            '-keypass', 'changeit',
            '-keystore', 'keystore.jks',
            '-storeType', 'JKS',
            '-storepass', 'changeit',
            '-ext', 'SAN=dns:lowkey-vault.local,dns:lowkey-vault,dns:*.localhost,dns:*.lowkey-vault,dns:*.lowkey-vault.local,dns:*.default.svc.cluster.local,dns:localhost,ip:127.0.0.1'

    doFirst {
        file("${project.projectDir}/src/main/resources/cert/keystore.jks").delete()
    }

    logging.captureStandardOutput LogLevel.INFO
    logging.captureStandardError LogLevel.ERROR
}
tasks.register('regenerateCert', Exec) {
    outputs.file("${project.projectDir}/src/main/resources/cert/keystore.p12")
    outputs.file("${project.projectDir}/src/main/resources/cert/keystore.jks")
    inputs.file("${project.projectDir}/src/main/resources/cert/keystore.jks")
    workingDir file("${project.projectDir}/src/main/resources/cert")
    outputs.upToDateWhen { false }
    dependsOn tasks.regenerateCertJks
    //convert to P12 using the old keytool to fix algorithm issues when used with old JDK (and still use P12)
    commandLine 'keytool',
            '-importkeystore',
            '-srckeystore', 'keystore.jks',
            '-srcstorepass', 'changeit',
            '-srcstoretype', 'JKS',
            '-destkeystore', 'keystore.p12',
            '-deststorepass', 'changeit',
            '-deststoretype', 'pkcs12'

    doFirst {
        file("${project.projectDir}/src/main/resources/cert/keystore.p12").delete()
    }
    doLast {
        file("${project.projectDir}/src/main/resources/cert/keystore.jks").delete()
    }

    logging.captureStandardOutput LogLevel.INFO
    logging.captureStandardError LogLevel.ERROR
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact bootJar
            artifactId = "${project.name}"
            pom {
                name = "${project.artifactDisplayName}"
                description = "${project.artifactDescription}"
                url = rootProject.ext.repoUrl
                packaging = "jar"
                licenses {
                    license {
                        name = rootProject.ext.licenseName
                        url = rootProject.ext.licenseUrl
                    }
                }
                developers {
                    developer {
                        id = rootProject.ext.maintainerId
                        name = rootProject.ext.maintainerName
                        url = rootProject.ext.maintainerUrl
                    }
                }
                scm {
                    connection = rootProject.ext.scmConnection
                    developerConnection = rootProject.ext.scmConnection
                    url = rootProject.ext.scmProjectUrl
                }
                withXml {
                    asNode().dependencies.findAll() {
                        def parent = it.parent()
                        parent.remove(it)
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
