plugins {
    id 'java'
    alias(libs.plugins.lombok)
}

group = "${rootProject.group}"

project.ext {
    artifactDisplayName = "Lowkey Vault - Client"
    artifactDescription = "HTTP Client provider for Lowkey Vault tests."
}

dependencies {
    implementation(libs.azure.security.keyvault.keys) {
        exclude(group: "io.netty")
    }
    implementation(libs.azure.security.keyvault.secrets) {
        exclude(group: "io.netty")
    }
    implementation(libs.azure.security.keyvault.certificates) {
        exclude(group: "io.netty")
    }
    implementation "com.fasterxml.jackson:jackson-bom:${libs.versions.jacksonBom.get()}@pom"
    implementation libs.bundles.jackson
    implementation libs.httpclient
    implementation libs.commons.codec
    compileOnly libs.findbugs.jsr305
    annotationProcessor libs.lombok
    testImplementation libs.mockito.core
    testImplementation libs.jupiter
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation libs.logback.classic
}

test {
    useJUnitPlatform()
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = "${project.name}"
            pom {
                name = "${project.artifactDisplayName}"
                description = "${project.artifactDescription}"
                url = rootProject.ext.repoUrl
                packaging = "jar"
                licenses {
                    license {
                        name = rootProject.ext.licenseName
                        url = rootProject.ext.licenseUrl
                    }
                }
                developers {
                    developer {
                        id = rootProject.ext.maintainerId
                        name = rootProject.ext.maintainerName
                        url = rootProject.ext.maintainerUrl
                    }
                }
                scm {
                    connection = rootProject.ext.scmConnection
                    developerConnection = rootProject.ext.scmConnection
                    url = rootProject.ext.scmProjectUrl
                }
                withXml {
                    asNode().dependencies.'*'.findAll() {
                        it.scope.text() == 'runtime'
                    }.each { it.scope*.value = 'compile' }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
