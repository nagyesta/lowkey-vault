plugins {
    id 'java'
    alias(libs.plugins.abort.mission)
    alias(libs.plugins.docker)
    alias(libs.plugins.docker.run)
}

version = rootProject.version
//noinspection GroovyUnusedAssignment
sourceCompatibility = '11'
repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

group = "${rootProject.group}"

dependencies {
    testImplementation project(":lowkey-vault-client")
    testImplementation libs.bouncycastle.bcpkix
    testImplementation libs.httpclient
    testImplementation libs.commons.codec
    testImplementation(libs.azure.security.keyvault.keys) {
        exclude(group: "io.netty")
    }
    testImplementation(libs.azure.security.keyvault.secrets) {
        exclude(group: "io.netty")
    }
    testImplementation(libs.azure.security.keyvault.certificates) {
        exclude(group: "io.netty")
    }
    testImplementation libs.bundles.spring.test
    testImplementation libs.bundles.cucumber
    testImplementation libs.abort.mission.cucumber
    testImplementation libs.findbugs.jsr305
}

task copyAppJar(type: Copy) {
    inputs.file(rootProject.project(":lowkey-vault-app").tasks.getByPath("bootJar").outputs.files.singleFile)
    outputs.file("${buildDir}/app/lowkey-vault.jar")
    from rootProject.project(":lowkey-vault-app").tasks.getByPath("bootJar").outputs.files.singleFile
    into file("${buildDir}/app/")
    rename {
        'lowkey-vault.jar'
    }
    dependsOn(":lowkey-vault-app:bootJar")
    dependsOn(":lowkey-vault-app:test")
}

docker {
    name "lowkey-vault:${rootProject.version}"
    tag 'dockerNagyesta', "nagyesta/lowkey-vault:${rootProject.version}"
    dockerfile file('src/docker/Dockerfile')
    files file("${buildDir}/app/lowkey-vault.jar")
    pull true
    noCache true
}
tasks.dockerPrepare.inputs.file("${buildDir}/app/lowkey-vault.jar")
tasks.dockerPrepare.dependsOn copyAppJar
clean.mustRunAfter dockerClean

dockerRun {
    name "lowkey-vault"
    image "lowkey-vault:${rootProject.version}"
    ports "8444:8443"
    daemonize true
    arguments '--rm'
    env 'LOWKEY_ARGS': '--LOWKEY_DEBUG_REQUEST_LOG=false ' +
            '--LOWKEY_VAULT_NAMES=certs-generic,keys-generic,keys-delete,secrets-generic,secrets-delete ' +
            '--LOWKEY_VAULT_ALIASES=keys-delete.localhost=keys-alias-delete.localhost:<port>,' +
            'secrets-delete.localhost=secrets-alias-delete.localhost:<port>'
}
tasks.dockerRun.dependsOn tasks.docker

test {
    outputs.file(file("$buildDir/reports/abort-mission/abort-mission-report.json"))
    outputs.dir(file("$buildDir/reports/cucumber"))
    systemProperty("cucumber.execution.parallel.enabled", System.getProperty("test.parallel"))
    systemProperty("cucumber.filter.tags", "not @ignore")
    useTestNG {
        systemProperty("abort-mission.report.directory", file("${buildDir}/reports/abort-mission/"))
        systemProperty("abort-mission.force.abort.evaluators", rootProject.ext.dockerAbortGroups)
        systemProperty("abort-mission.suppress.abort.evaluators", rootProject.ext.dockerSuppressGroups)
    }
}
test.outputs.upToDateWhen { false }
test.dependsOn tasks.dockerRun
test.finalizedBy tasks.dockerStop

abortMission {
    toolVersion libs.versions.abortMission.get()
}

task publish {
    dependsOn(build)
    dependsOn("dockerPushDockerNagyesta")
}
